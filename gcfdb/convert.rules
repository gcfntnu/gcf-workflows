#-*- mode:snakemake -*-
"""Converters between common fileformats
"""

rule pyensembl_version:
    output:
        'pyensembl.version'
    singularity:
        'docker://' + config['docker']['pyensembl']
    params:
        script = srcdir('scripts/pyensembl_version.py'),
        docker = config['docker']['pyensembl']
    shell:
        'python {params.script} {params.docker} > {output}'

rule convert_gtf2gene_csv:
    input:
        gtf = join('{ref_dir}', 'anno', 'genes.gtf')
    params:
        script = srcdir('scripts/gtf2genes.py'),
    output:
        tsv = join('{ref_dir}', 'anno', 'genes.tsv')
    singularity:
        'docker://' + config['docker']['pyensembl']
    shell:
        """
        python {params.script} {input.gtf} > {output.tsv}
        """
        
rule convert_gtf2transcript_csv:
    input:
        join('{ref_dir}', 'anno', 'genes.gtf')
    params:
        script = srcdir('scripts/gtf2tx.py')
    output:
        join('{ref_dir}', 'anno', 'transcripts.tsv')
    singularity:
        'docker://' + config['docker']['pyensembl']
    shell:
        'python {params.script} {input} > {output}'    

rule convert_gtf2txg:
    input:
        rules.convert_gtf2gene_csv.output
    output:
        join('{ref_dir}', 'anno', 'tx2gene.tsv')
    shell:
        'cut -f1,10 {input} > {output}'
        
rule convert_fasta2sequence_dict:
    input:
        join('{ref_dir}', 'fasta', 'genome.fa')
    output:
        join('{ref_dir}', 'fasta', 'genome.dict')
    singularity:
        'docker://' + config['docker']['picard_gatk']
    shell:
        'picard CreateSequenceDictionary REFERENCE={input} OUTPUT={output}'
        
rule convert_gtf2bed12:
    input:
        join('{ref_dir}', 'anno', 'genes.gtf')
    output:
        join('{ref_dir}', 'anno', 'genes.bed12')
    params:
        tmpfile = join(TMPDIR, 'gene.genepred')
    singularity:
        'docker://' + config['docker']['ucsc-scripts']
    shell:
        """
        gtfToGenePred -genePredExt -geneNameAsName2 {input} {params}
        genePredToBed {params} {output}
        rm {params.tmpfile}
        """
        
rule convert_gtf2intervals:
    input:
        join('{ref_dir}', 'anno', 'genes.gtf')
    output:
        join('{ref_dir}', 'anno', 'genes.intervals')
    shell:
        ''
        
rule convert_gtf2refflat:
    input:
        join('{ref_dir}', 'anno', 'genes.gtf')
    output:
         join('{ref_dir}', 'anno', 'genes.refflat.gz')
    singularity:
        'docker://' + config['docker']['ucsc-scripts']
    shadow:
        'minimal'
    shell:
        """
        gtfToGenePred -genePredExt -geneNameAsName2 {input} dummy.txt
        paste <(cut -f 12 dummy.txt) <(cut -f 1-10 dummy.txt) > refFlat.txt
        gzip refFlat.txt
        mv refFlat.txt.gz {output}
        """
        
rule convert_gtf_transcriptome_gffread:
    input:
        gtf = join('{ref_dir}', 'anno', 'genes.gtf'),
        genome = join('{ref_dir}', 'fasta', 'genome.fa')
    output:
        join('{ref_dir}', 'fasta', 'gtf.gffread.transcripts.fa')
    singularity:
        'docker://' + config['docker']['gffread']
    shell:
        'gffread -w {output} -g {input.genome} {input.gtf}'
        
rule convert_gtf_transcriptome_rsem:
    input:
        gtf = join('{ref_dir}', 'anno', 'genes.gtf'),
        genome = join('{ref_dir}', 'fasta', 'genome.fa')
    params:
        base = join('{ref_dir}', 'fasta', 'gtf.rsem')
    singularity:
        'docker://' + config['docker']['rsem']
    output:
        join('{ref_dir}', 'fasta', 'gtf.rsem.transcripts.fa')
    shell:
        'rsem-prepare-reference --gtf {input.gtf} {input.genome} {params.base}'
