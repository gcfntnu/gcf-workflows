#-*- mode:snakemake -*-
"""10xgenomics external data and reference/index builds

support.10xgenomics.com/single-cell-gene-expression/software/downloads

"""

_ASSEMBLY = {'homo_sapiens': 'GRCh38',
             'mus_musculus': 'GRCm38',
             'homo_sapiens__mus_musculus': 'GRCh38_GRCm38'}

# config
ORG = config['organism']
if ORG not in ['homo_sapiens', 'mus_musculus', 'homo_sapiens__mus_musculus']:
    logger.warning('No 10x prebuild for org: {}'.format(ORG))

DB_CONF = config['db'].get('10xgenomics', {})
TX_RELEASE = DB_CONF.get('release', '2020-A')
TX_ASSEMBLY = config['db']['assembly'] = DB_CONF['assembly'] = _ASSEMBLY.get(ORG, '')

def get_10x_url(wildcards):
    base = 'https://cf.10xgenomics.com/supp/cell-exp'
    if wildcards.organism == 'homo_sapiens':
        if not wildcards.assembly == 'GRCh38':
            raise KeyError('10x homo sapiens genome is only GRCh38 assembly')
        fn = 'refdata-gex-{}-{}.tar.gz'.format(wildcards.assembly, wildcards.release)
    elif wildcards.organism == 'mus_musculus':
        if not wildcards.assembly == 'GRCm38':
            raise KeyError('10x mus musculus genome is only GRCm38 assembly')
        assembly = 'mm10'
        fn = 'refdata-gex-{}-{}.tar.gz'.format(assembly, wildcards.release)
    elif wildcards.organism == 'homo_sapiens__mus_musculus':
        fn = 'refdata-gex-GRCh38-and-mm10-2020-A.tar.gz'
    else:
        raise ValueError('`{}` is not a supported organism for 10x prebuilds'.format(wildcards.organism))
    return join(base, fn)

def get_10x_prefix(wildcards):
    base = join(EXT_DIR, '10xgenomics', 'release-{}', '{}', '{}')
    prefix = base.format(wildcards.release, wildcards.organism, wildcards.assembly)
    return prefix

    
rule txgenomics_org_prebuild:
    params:
        url = get_10x_url,
        out = get_10x_prefix,
        proxy = ''
    output:
        json = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'reference.json'),
        gtf = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'anno', 'genes.gtf'),
        fasta = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'fasta', 'genome.fa'),
        star_index = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'star', 'SA')
    log:
        genome = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'logs', 'genome.log'),
        gtf = join(EXT_DIR, '10xgenomics', 'release-{release}', '{organism}', '{assembly}', 'logs', 'gtf.log')
    shell:
        """
        curl {params.proxy} {params.url} | tar xvz --strip-components=1 -C {params.out}
        ln -s {params.out}/genes/genes.gtf {params.out}/anno
        echo "10xGenomics Genome,release-{wildcards.release},{params.url},`date -I`" > {log.genome}
        echo "10xGenomics GTF ,release-{wildcards.release},{params.url},`date -I`" > {log.gtf}
        """


rule human_txgenomics_common_snps:
    params:
        proxy = config.get('proxy', {}).get('wget', ''),
        date = datetime.now().strftime("%d-%m-%Y"), 
        url = join(config.get('winecellar', {}).get('url', ''), 'thousand_genomes', 'common_variants_grch38_chr.vcf')
    output:
        join(EXT_DIR, '10xgenomics', 'release-{}'.format(TX_RELEASE), 'homo_sapiens', 'GRCh38', 'anno', 'common_variants.vcf')
    log:
        join(EXT_DIR, '10xgenomics', 'release-{}'.format(TX_RELEASE), 'homo_sapiens', 'GRCh38', 'logs', 'common_variants.log')
    shell:
        'wget {params.proxy} -O {output} {params.url} && '
        'echo "1000genomes common snps,NA,{params.url},{params.date}" > {log} '
        
