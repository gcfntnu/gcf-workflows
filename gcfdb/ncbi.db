#-*- mode:snakemake -*-
"""NCBI data mainly fetched using efetch
"""

rule ncbi_homo_sapiens_rrna:
    params:
        url_cru = 'https://www.ncbi.nlm.nih.gov/nuccore/555853',
        url_5S = 'https://www.ncbi.nlm.nih.gov/nuccore/23898',
        date = datetime.now().strftime("%d-%m-%Y")
    output:
        fasta = join(EXT_DIR, 'ncbi', 'homo_sapiens', 'fasta', 'rrna.fa')
    log:
        join(EXT_DIR, 'ncbi', 'homo_sapiens', 'logs', 'rrna.log')
    singularity:
        'docker://' + config['docker']['entrez-direct']
    shell:
        """
        efetch -db nuccore -id U13369.1 -format fasta > {output.fasta}
        efetch -db nuccore -id X12811.1 -format fasta >> {output.fasta}
        echo "ncbi nucore,U13369.1|X12811.1,{params.url_cru}|{params.url_5S},{params.date}" > {log}
        """

rule ncbi_mus_musculus_rrna:
    params:
        url_cru = 'https://www.ncbi.nlm.nih.gov/nuccore/bk000964.3',
        url_5S = 'https://www.ncbi.nlm.nih.gov/nuccore/NR_030686.1',
        date = datetime.now().strftime("%d-%m-%Y")
    output:
        join(EXT_DIR, 'ncbi', 'mus_musculus', 'fasta', 'rrna.fa')
    log:
        join(EXT_DIR, 'ncbi', 'mus_musculus', 'logs', 'rrna.log')
    singularity:
        'docker://' + config['docker']['entrez-direct']
    shell:
        """
        efetch -db nuccore -id bk000964.3 -format fasta > {output}
        efetch -db nuccore -id NR_030686.1 -format fasta >> {output}
        echo "ncbi nucore,bk000964.3,{params.url_cru}|{params.url_5S},{params.date}" > {log}
        """

rule ncbi_sars_cov2:
    params:
        url = 'https://www.ncbi.nlm.nih.gov/nuccore/NC_045512',
        date = datetime.now().strftime("%d-%m-%Y")
    output:
        join(EXT_DIR, 'ncbi', 'NC_045512.2', 'fasta', 'NC_045512.2.fa')
    log:
        join(EXT_DIR, 'ncbi', 'NC_045512.2', 'logs', 'NC_045512.2.log')
    singularity:
        'docker://' + config['docker']['entrez-direct']
    shell:
        """
        efetch -db nuccore -id NC_045512 -format fasta > {output}
        echo "ncbi nucore,{params.url}|{params.url},{params.date}" > {log}
        """

rule ncbi_streptococcus_agalactiae_genome:
    params:
        url = 'ftp.ncbi.nlm.nih.gov/genomes/refseq/bacteria/Streptococcus_agalactiae/representative/GCF_900638415.1_57635_F01',
        date = datetime.now().strftime("%d-%m-%Y"),
        protocol = 'https:'
    output:
        fasta = join(EXT_DIR, 'ncbi', 'streptococcus_agalactiae', 'fasta', 'streptococcus_agalactiae.fa'),
        gff = join(EXT_DIR, 'ncbi', 'streptococcus_agalactiae', 'anno', 'streptococcus_agalactiae.gff'),
        gtf = join(EXT_DIR, 'ncbi', 'streptococcus_agalactiae', 'anno', 'streptococcus_agalactiae.gtf')
    log:
        join(EXT_DIR, 'ncbi', 'streptococcus_agalactiae', 'logs', 'streptococcus_agalactiae.log')
    shadow:
        'minimal'
    shell:
        """
        rsync -av --exclude '*.txt' {params.protocol}//{params.url}/ .
        gunzip *.fna.gz -c > {output.fasta}
        gunzip *.gff.gz -c > {output.gff}
        gunzip *.gtf.gz -c > {output.gtf}
        echo "ncbi bacteria,{params.url},{params.date}" > {log}
        """

rule ncbi_cervus_elaphus_hippelaphus:
    params:
        url = 'ftp.ncbi.nlm.nih.gov/genomes/all/GCA/002/197/005/GCA_002197005.1_CerEla1.0/',
        date = datetime.now().strftime("%d-%m-%Y"),
        protocol = 'https:'
    output:
        fasta = join(EXT_DIR, 'ncbi', 'cervus_elaphus_hippelaphus', 'fasta', 'genome.fa'),
        gff = join(EXT_DIR, 'ncbi', 'cervus_elaphus_hippelaphus', 'anno', 'genes.gff'),
        gtf = join(EXT_DIR, 'ncbi', 'cervus_elaphus_hippelaphus', 'anno', 'genes.gtf')
    log:
        join(EXT_DIR, 'ncbi', 'cervus_elaphus_hippelaphus', 'logs', 'cervus_elaphus_hippelaphus.log')
    shadow:
        'minimal'
    shell:
        """
        rsync -av --exclude '*.txt' {params.protocol}//{params.url}/ .
        gunzip GCA_002197005.1_CerEla1.0_genomic.fna.gz -c > {output.fasta}
        gunzip GCA_002197005.1_CerEla1.0_genomic.gff.gz -c > {output.gff}
        gunzip GCA_002197005.1_CerEla1.0_genomic.gtf.gz -c > {output.gtf}
        echo "ncbi genomes,{params.url},{params.date}" > {log}
        """
        
rule ncbi_db_all:
    input:
        join(EXT_DIR, 'ncbi', 'homo_sapiens', 'fasta', 'rrna.fa'),
        join(EXT_DIR, 'ncbi', 'mus_musculus', 'fasta', 'rrna.fa'),
        join(EXT_DIR, 'ncbi', 'homo_sapiens', 'index', 'rrna', 'bowtie', 'rrna.1.ebwt'),
        join(EXT_DIR, 'ncbi', 'mus_musculus', 'index', 'rrna', 'bowtie', 'rrna.1.ebwt'),
        join(EXT_DIR, 'ncbi', 'homo_sapiens', 'index', 'rrna', 'bowtie2', 'rrna.1.bt2'),
        join(EXT_DIR, 'ncbi', 'mus_musculus', 'index', 'rrna', 'bowtie2', 'rrna.1.bt2'),
        join(EXT_DIR, 'ncbi', 'NC_045512.2', 'fasta', 'NC_045512.2.fa')

        
