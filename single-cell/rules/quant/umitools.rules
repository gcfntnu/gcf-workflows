#-*- mode:snakemake -*-

extra_conf_fn = srcdir('umitools.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh, Loader=Loader) or {}
        update_config2(config, c)

# force concat of fastq files
#config['filter']['skip'] = False

include:
    'cellranger.rules'
    
#config
ORG = config.get('organism', 'homo_sapiens')
UMI_INTERIM= join(QUANT_INTERIM, 'umitools')

rule umitools_whitelist:
    input:
        R1 = get_filtered_fastq_R1
    params:
        bc_pattern = LIBPREP['umi_tools']['chemistry'],
        extra_args = '--plot-prefix ' + UMI_INTERIM + '/{sample}/{sample}'
    output:
        join(UMI_INTERIM, '{sample}','whitelist.txt')
    singularity:
        'docker://' + config['docker']['umi_tools']
    shell:
        'umi_tools whitelist '
        '--bc-pattern {params.bc_pattern} '
        '{params.extra_args} '
        '--stdin {input.R1} '
        '--log2stderr '
        '> {output}'

rule umitools_extract:
    input:
        R1 = get_filtered_fastq_R1,
        R2 = get_filtered_fastq_R2,
        whitelist = rules.umitools_whitelist.output
    params:
        '--bc-pattern={} --filter-cell-barcode '.format(LIBPREP['umi_tools']['chemistry'])
    output:
        R1 = join(UMI_INTERIM, '{sample}', '{sample}_R1.fastq.gz'),
        R2 = join(UMI_INTERIM, '{sample}', '{sample}_R2.fastq.gz')
    singularity:
        'docker://' + config['docker']['umi_tools']
    shell:
        'umi_tools extract '
        '--stdin {input.R1} '
        '--read2-in {input.R2} '
        '--stdout {output.R1} '
        '--read2-out {output.R2} '
        '--whitelist {input.whitelist} '
        '{params} '

rule umitools_align_star:
    input:
        R2 = rules.umitools_extract.output.R2,
        ref = join(REF_DIR, 'star', 'SA')
    output:
        bam = join(UMI_INTERIM, '{sample}', '{sample}.Aligned.sortedByCoord.out.bam')
    singularity:
        'docker://' + config['docker']['star']
    params:
        ref = join(REF_DIR, 'star'),
        out_prefix = join(UMI_INTERIM, '{sample}', '{sample}.'),
        args = '--readFilesCommand zcat --outFilterMultimapNmax 1 --outSAMtype BAM SortedByCoordinate '
    threads:
        16
    shell:
        'STAR '
        '--readFilesIn {input.R2} '
        '--genomeDir {params.ref} '
        '--runThreadN {threads} '
        '--outFileNamePrefix {params.out_prefix} ' 
        '{params.args}'

rule umitools_assign_genes:
    input:
        bam = join(UMI_INTERIM, '{sample}', '{sample}.Aligned.sortedByCoord.out.bam'),
        gtf = join(REF_DIR, 'genes', 'genes.gtf')
    singularity:
        'docker://' + config['docker']['subread']
    output:
        out = join(UMI_INTERIM, '{sample}', '{sample}.gene_assignment.txt'),
        bam = join(UMI_INTERIM, '{sample}', '{sample}.Aligned.sortedByCoord.out.bam.featureCounts.bam')
    threads:
        16
    shell:
        'featureCounts '
        '-a {input.gtf} ' 
        '-o {output.out} '
        '-R BAM '
        '-T {threads} '
        '{input.bam} '

rule umitools_sort_bam:
    input:
        join(UMI_INTERIM, '{sample}', '{sample}.Aligned.sortedByCoord.out.bam.featureCounts.bam')
    output:
        join(UMI_INTERIM, '{sample}', '{sample}.Assigned.sorted.bam')
    singularity:
        'docker://' + config['docker']['samtools']
    shell:
        'samtools sort {input} -o {output}'

rule umitools_index_bam:
    input:
        join(UMI_INTERIM, '{sample}', '{sample}.Assigned.sorted.bam')
    output:
        join(UMI_INTERIM, '{sample}', '{sample}.Assigned.sorted.bam.bai')
    singularity:
        'docker://' + config['docker']['umi_tools']
    shell:
        'samtools index {input}'
    
rule umitools_quant:
    input:
       bam = join(UMI_INTERIM, '{sample}', '{sample}.Assigned.sorted.bam'),
       index = join(UMI_INTERIM, '{sample}', '{sample}.Assigned.sorted.bam.bai')
    output:
        join(UMI_INTERIM,'{sample}' ,'counts.tsv.gz')
    params:
        '--per-gene --gene-tag=XT --assigned-status-tag=XS --per-cell '
    singularity:
        'docker://' + config['docker']['umi_tools']
    shell:
        'umi_tools count '
        '{params} '
        '-I {input.bam} '
        '-S {output} '

rule umitools_scanpy:
    input:
      rules.umitools_quant.output
    params:
        script = srcdir('scripts/convert_scanpy.py')
    output:
        join(UMI_INTERIM, '{sample}', 'scanpy', 'adata.h5ad')
    singularity:
        'docker://' + config['docker']['scanpy']
    shell:
        'python {params.script} -i {input} -o {output} '

rule umitools_seurat:
    input:
      rules.umitools_quant.output
    params:
        script = srcdir('scripts/umitools_seurat.R')
    output:
        join(UMI_INTERIM, 'seurat', '{sample}', '{sample}.rds')
    singularity:
        'docker://' + config['docker']['seurat']
    shell:
        'Rscript {params.script} -i {input} -o {output} '
